buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.12.1'
    }
}
plugins {
    id 'java'
    id 'maven'

    id 'net.ellune.blossom' version '1.0.1'
}
apply plugin: 'com.github.hierynomus.license'

group = 'xyz.lexteam.ygd'

blossom {
    replaceToken '@project.version@',
            project.version, 'src/main/java/uk/jamierocks/lexteam/ygd/core/Game.java'
}

dependencies {
    compile 'xyz.lexteam:eventbus:1.3'
    compile 'xyz.lexteam:meta:1.0.0-SNAPSHOT'
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'com.google.guava:guava:19.0'
    compile 'com.flowpowered:flow-math:1.0.1'
    compile 'ninja.leaping.configurate:configurate-hocon:2.0'
}

license {
    header rootProject.file('HEADER.txt')
    include '**/*.java'
    include '**/*.groovy'

    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

task javadocJar(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task sourcesJar(type: Jar, dependsOn: 'classes') {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            // Lexteam Maven Repository
            repository(url: 'http://lex1.lexteam.xyz:8888/repository/private-internal/') {
                authentication(userName: lexRepoUser, password: lexRepoPass)
            }

            snapshotRepository(url: 'http://lex1.lexteam.xyz:8888/repository/private-snapshots/') {
                authentication(userName: lexRepoUser, password: lexRepoPass)
            }

            pom {
                artifactId = project.archivesBaseName.toLowerCase()

                project {
                    name = project.archivesBaseName
                    packaging = 'jar'

                    scm {
                        url = 'https://git.lexteam.xyz/lex-game/Game'
                        connection = 'scm:git:https://git.lexteam.xyz/lex-game/Game.git'
                        developerConnection = 'scm:git:git@git.lexteam.xyz:lex-game/Game.git'
                    }

                    issueManagement {
                        system = 'Gogs'
                        url = 'https://git.lexteam.xyz/lex-game/Game/issues'
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'http://opensource.org/licenses/MIT'
                            distribution = 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'jamierocks'
                            name 'Jamie Mansfield'
                            email 'jamie@lexteam.xyz'
                        }
                        developer {
                            id 'charlesstewart'
                            name 'Tom Drever'
                            email 'tom@lexteam.xyz'
                        }
                        developer {
                            id 'UNKDevE'
                            name 'Ethan Riley'
                            email 'ethan@lexteam.xyz'
                        }
                    }
                }
            }
        }
    }
}
